/*
 * (#)build.gradle  0.7.0   12/19/2024
 * (#)build.gradle  0.6.0   11/18/2024
 * (#)build.gradle  0.5.0   11/15/2024
 * (#)build.gradle  0.4.0   11/15/2024
 * (#)build.gradle  0.3.0   11/13/2024
 * (#)build.gradle  0.2.0   11/08/2024
 * (#)build.gradle  0.1.0   11/04/2024
 *
 * @author   Jonathan Parker
 * @version  0.7.0
 * @since    0.1.0
 *
 * MIT License
 *
 * Copyright (c) 2024 Jonathan M. Parker
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

/*
 * The following system property must be set in HOME/.gradle/gradle.properties:
 *   systemProp.macosArchitecture=intel -or-
 *   systemProp.macosArchitecture=silicon
 */

plugins {
    id 'application'
    id 'jacoco'  // For code coverage
    id 'java'
    id 'maven-publish'
}

apply plugin : 'java'

ext {
    execDescription = 'Run the main class with JavaExecTask'
    execGroup = 'Execution'
    javaMainClass = 'net.jmp.spring.java.app.Bootstrap'
    logConfigFile = '-Dlogback.configurationFile=config/logback.xml'
    macosArchitecture = System.getProperty('macosArchitecture')
    useAssertions = '-ea'
}

println "The macOS architecture is ${macosArchitecture}"

application {
    mainClass.set(javaMainClass)
}

repositories {
    mavenLocal()
    mavenCentral()  // url = uri('https://repo.maven.apache.org/maven2/')

    maven {
        url = uri('http://maven.repository.redhat.com/techpreview/all')

        allowInsecureProtocol = true
    }

    maven {
        url = uri('http://localhost:8081/nexus/content/repositories/releases')

        allowInsecureProtocol = true
    }
}

dependencies {
    runtimeOnly         (variantOf(libs.io.netty.netty.resolver.dns.native.macos) { classifier("osx-aarch_64") })
    runtimeOnly         (libs.bundles.logback)
    runtimeOnly         (libs.com.mysql.mysql.connection.j)
    implementation      (libs.bundles.jmp)
    implementation      (libs.bundles.mongodb)
    implementation      (libs.bundles.redis)
    implementation      (libs.bundles.spring)
    implementation      (libs.com.fasterxml.jackson.core.jackson.databind)
    implementation      (libs.org.slf4j.slf4j.api)
    testRuntimeOnly     (libs.org.junit.platform.junit.platform.launcher)
    testImplementation  (libs.org.assertj.assertj.core)
    testImplementation  (platform(libs.org.junit.junit.bom))
    testImplementation  (libs.bundles.junit.jupiter)
}

group = 'net.jmp.spring.java.app'
version = '0.7.0'
description = 'Spring-Service'

println "The groupId is ${project.group}"
println "The artifactId is ${project.name}" // Set in settings.gradle
println "The version is ${project.version}"

java.sourceCompatibility = JavaVersion.VERSION_23

java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

    // Enable JaCoCo test coverage
    finalizedBy jacocoTestReport
}

// Configure JaCoCo for test coverage reports
jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(23)
}

tasks.withType(Javadoc).configureEach {
    CoreJavadocOptions cjo = options as CoreJavadocOptions
    StandardJavadocDocletOptions sjdo = options as StandardJavadocDocletOptions

    cjo.encoding("UTF-8")
    cjo.addStringOption("source", "23")
    cjo.memberLevel = JavadocMemberLevel.PRIVATE

    sjdo.version(true)
}

/*
 * Execute with - gradle execute instead of gradle run
 */

tasks.register('execute', JavaExec) {
    allJvmArgs = [useAssertions, logConfigFile] as List<String>
    classpath = sourceSets.main.runtimeClasspath
    description = execDescription
    group = execGroup
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
    mainClass = javaMainClass
}
